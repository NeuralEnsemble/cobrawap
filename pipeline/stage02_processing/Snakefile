# Stage 02 Processing

configfile: "config.yaml"
report: "report.rst"

import os
import sys
utils_path = '../scripts/'
sys.path.append(utils_path)

from settings import output_path
from utils import parse_plot_channels

STAGE_NAME = config["STAGE_NAME"]
STAGE_OUTPUT = config["STAGE_OUTPUT"]
NEO_FORMAT = config["NEO_FORMAT"]
USE_LINK_AS_STAGE_OUTPUT = config["USE_LINK_AS_STAGE_OUTPUT"]
ADD_UTILS = f"export PYTHONPATH='$PYTHONPATH:{utils_path}'"
OUTPUT_DIR = os.path.join(output_path, STAGE_NAME)

if 'STAGE_INPUT' in config and config['STAGE_INPUT'] is not None:
    STAGE_INPUT = config["STAGE_INPUT"]
else:
    STAGE_INPUT = os.path.join(output_path, 'stage01_data_entry', 'data.nix')

PLOT_TSTART = config["PLOT_TSTART"]
PLOT_TSTOP = config["PLOT_TSTOP"]
PLOT_CHANNELS = parse_plot_channels(config["PLOT_CHANNELS"], STAGE_INPUT)
PLOT_FORMAT = config["PLOT_FORMAT"]

BLOCK_ORDER = config["BLOCK_ORDER"]
NORMALIZE_BY = config["NORMALIZE_BY"]
HIGHPASS_FREQ = config["HIGHPASS_FREQ"]
LOWPASS_FREQ = config["LOWPASS_FREQ"]
FILTER_ORDER = config["FILTER_ORDER"]
FILTER_FUNCTION = config["FILTER_FUNCTION"]
PSD_FREQ_RES = config["PSD_FREQ_RES"]
PSD_OVERLAP = config["PSD_OVERLAP"]
DETRENDING_ORDER = config["DETRENDING_ORDER"]
INTENSITY_THRESHOLD = config["INTENSITY_THRESHOLD"]
MUA_HIGHPASS_FREQ = config["MUA_HIGHPASS_FREQ"]
MUA_LOWPASS_FREQ = config["MUA_LOWPASS_FREQ"]
logMUA_RATE = config["logMUA_RATE"]
FFT_SLICE = config["FFT_SLICE"]

#### Housekeeping ####

def locate(str_list, string):
    if string in str_list:
        return [i for i, el in enumerate(str_list) if el == string][0]
    else:
        raise ValueError("Can't find rule '{}'! Please check the spelling \
                          and the config file.".format(string))

def input_file(wildcards):
    if hasattr(wildcards, 'rule_name'):
        idx = locate(BLOCK_ORDER, wildcards.rule_name)
        if idx:
            return os.path.join(OUTPUT_DIR, BLOCK_ORDER[idx-1],
                                BLOCK_ORDER[idx-1]+'.'+NEO_FORMAT)
    elif len(BLOCK_ORDER):
        return os.path.join(OUTPUT_DIR, BLOCK_ORDER[-1],
                            BLOCK_ORDER[-1]+'.'+NEO_FORMAT)
    return os.path.join(OUTPUT_DIR, STAGE_INPUT)

rule all:
    input:
        data = input_file,
        img = [os.path.join(OUTPUT_DIR,
               f'processed_trace_{PLOT_TSTART}-{PLOT_TSTOP}s_channel{channel}.{PLOT_FORMAT}')
               for channel in PLOT_CHANNELS],
        configfile = 'config.yaml'
    params:
        command = 'ln -s' if USE_LINK_AS_STAGE_OUTPUT else 'cp'
    output:
        data = os.path.join(OUTPUT_DIR, STAGE_OUTPUT),
    shell:
        """
        {params.command} "{input.data}" "{output.data}"
        """

rule plot_processed_traces:
    input:
        stage_input = STAGE_INPUT,
        stage_output = input_file,
        script = 'scripts/plot_processed_trace.py'
    output:
        img = report(os.path.join(OUTPUT_DIR,
                                  'processed_trace_{t_start}-{t_stop}s_channel{channel}.'+PLOT_FORMAT))
    shell:
        """
        {ADD_UTILS}
        python {input.script} --original_data "{input.stage_input}" \
                              --processed_data "{input.stage_output}" \
                              --output "{output.img}" \
                              --t_start {wildcards.t_start} \
                              --t_stop {wildcards.t_stop} \
                              --channel {wildcards.channel}
        """

rule clear:
    output:
        temp(os.path.join(OUTPUT_DIR, 'clear.done'))
    params:
        block_folder = ['"'+os.path.join(OUTPUT_DIR, block)+'"'
                        for block in BLOCK_ORDER]
    shell:
        """
        rm -rf {params.block_folder}
        touch "{output}"
        """

#### Blocks ####

rule background_substraction:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/background_substraction.py'
    output:
        data = os.path.join(OUTPUT_DIR, 'background_substraction',
                            '{rule_name}.'+NEO_FORMAT),
        img = report(os.path.join(OUTPUT_DIR, '{rule_name}',
                                  'background.'+PLOT_FORMAT)),
        array = os.path.join(OUTPUT_DIR, '{rule_name}',
                             'background.npy')
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --output_img "{output.img}" \
                              --output_array "{output.array}"
        """

rule normalization:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/normalization.py'
    params:
        normalize_by = NORMALIZE_BY
    output:
        data = os.path.join(OUTPUT_DIR, 'normalization',
                            '{rule_name}.'+NEO_FORMAT)
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --normalize_by {params.normalize_by}
        """

rule detrending:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/detrending.py'
    params:
        order = DETRENDING_ORDER
    output:
        data = os.path.join(OUTPUT_DIR, 'detrending',
                            '{rule_name}.'+NEO_FORMAT)
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --order {params.order}
        """

rule frequency_filter:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        img = os.path.join(OUTPUT_DIR,
                           f'frequency_filter_power_spectrum.{PLOT_FORMAT}'),
        script = 'scripts/frequency_filter.py',
    params:
        highpass_freq = HIGHPASS_FREQ,
        lowpass_freq = LOWPASS_FREQ,
        order = FILTER_ORDER,
        filter_function = FILTER_FUNCTION,
    output:
        data = os.path.join(OUTPUT_DIR, 'frequency_filter',
                            '{rule_name}.'+NEO_FORMAT)
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --highpass_freq {params.highpass_freq} \
                              --lowpass_freq {params.lowpass_freq} \
                              --order {params.order} \
                              --filter_function {params.filter_function}
        """

rule plot_power_spectrum:
    input:
        data = input_file,
        script = 'scripts/plot_power_spectrum.py'
    params:
        highpass_freq = HIGHPASS_FREQ,
        lowpass_freq = LOWPASS_FREQ,
        psd_freq_res = PSD_FREQ_RES,
        psd_overlap = PSD_OVERLAP
    output:
        img = report(os.path.join(OUTPUT_DIR, '{rule_name}_'\
                                  +f'power_spectrum.{PLOT_FORMAT}'))
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.img}" \
                              --highpass_freq {params.highpass_freq} \
                              --lowpass_freq {params.lowpass_freq} \
                              --psd_freq_res {params.psd_freq_res} \
                              --psd_overlap {params.psd_overlap} \
        """

rule roi_selection:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/roi_selection.py'
    params:
        intensity_threshold = INTENSITY_THRESHOLD
    output:
        data = os.path.join(OUTPUT_DIR, 'roi_selection',
                            '{rule_name}.'+NEO_FORMAT),
        img = report(os.path.join(OUTPUT_DIR, 'roi_selection',
                                    '{rule_name}.'+PLOT_FORMAT))
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --output_img "{output.img}" \
                              --intensity_threshold {params.intensity_threshold}
        """

rule logMUA_estimation:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/logMUA_estimation.py'
    params:
        highpass_freq = MUA_HIGHPASS_FREQ,
        lowpass_freq = MUA_LOWPASS_FREQ,
        logMUA_rate = logMUA_RATE,
        fft_slice = FFT_SLICE,
        psd_overlap = PSD_OVERLAP,
        plot_tstart = PLOT_TSTART,
        plot_tstop = PLOT_TSTOP,
        plot_channels = PLOT_CHANNELS,
        img = ','.join([os.path.join(OUTPUT_DIR, 'logMUA_estimation',
                                     f'logMUA_trace_{PLOT_TSTART}-{PLOT_TSTOP}s'
                                    +f'_channel{channel}.{PLOT_FORMAT}')
                                     for channel in PLOT_CHANNELS]),
    output:
        data = os.path.join(OUTPUT_DIR, 'logMUA_estimation',
                            '{rule_name}.'+NEO_FORMAT),
        img = report(os.path.join(OUTPUT_DIR, '{rule_name}',
                                  f'logMUA_trace_{PLOT_TSTART}-{PLOT_TSTOP}s'
                                 +f'_channel{PLOT_CHANNELS[0]}.{PLOT_FORMAT}'))
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}" \
                              --output_img "{params.img}" \
                              --highpass_freq {params.highpass_freq} \
                              --lowpass_freq {params.lowpass_freq} \
                              --logMUA_rate {params.logMUA_rate} \
                              --fft_slice {params.fft_slice} \
                              --psd_overlap {params.psd_overlap} \
                              --t_start {params.plot_tstart} \
                              --t_stop {params.plot_tstop} \
                              --channels {params.plot_channels}
        """

rule phase_transform:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/phase_transform.py'
    output:
        data = os.path.join(OUTPUT_DIR, 'phase_transform',
                            '{rule_name}.'+NEO_FORMAT)
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}"
        """

rule zscore:
    input:
        clear = os.path.join(OUTPUT_DIR, 'clear.done'),
        data = input_file,
        script = 'scripts/z_score.py'
    output:
        data = os.path.join(OUTPUT_DIR, 'zscore',
                            '{rule_name}.'+NEO_FORMAT)
    shell:
        """
        {ADD_UTILS}
        python {input.script} --data "{input.data}" \
                              --output "{output.data}"
        """
